package be.threads;

import lime.app.Future;

import flixel.FlxG;

import funkin.modding.module.ScriptedModule;
import funkin.play.PlayState;

typedef ThreadData = {
	/**
	 * The associated future with the thread.
	 */
	future:Future,
	
	/**
	 * Arbitrary data to share with the main thread for callbacks.
	 */
	data:Dynamic,
	
	/**
	 * The function to run in the thread.
	 * 
	 * Try to avoid interacting with other scripts as much as possible,
	 * but if you really have to do it, check against `data.panic` often.
	 * 
	 * Also, NEVER trace anything to console from a thread!
	 * This leads to unpredictable behavior and some variables being dropped.
	 * 
	 * @param data The thread data.
	 */
	work:(ThreadData)->Void,
	
	/**
	 * Prematurely "end" the thread with an error and trigger the `onError` callback.
	 * The thread will still run, but will be removed from the active list and marked as panicking.
	 * 
	 * @param result The error value.
	 */
	error:(Dynamic)->Void,
	
	/**
	 * Prematurely "end" the thread with a result and trigger the `onComplete` callback.
	 * The thread will still run, but will be removed from the active list.
	 * 
	 * @param result The result value.
	 */
	complete:(Dynamic)->Void,
	
	/**
	 * Callback for when the thread errors.
	 * 
	 * @param data The thread data.
	 * @param err The error value, if any.
	 */
	onError:(ThreadData, Dynamic)->Void,
	
	/**
	 * Callback for when the thread completes.
	 * 
	 * @param data The thread data.
	 * @param progress The progress value. Defaults to `0`.
	 * @param total The total value. Defaults to `progress` and cannot be less than `progress`.
	 */
	onComplete:(ThreadData, Int, Int)->Void,
	
	/**
	 * Whether the thread is panicking.
	 * Check against this often near unsafe code and return if `true`.
	 */
	panic:Bool
}

/**
 * Data for creating a thread.
 */
typedef ThreadParams = {
	/**
	 * Arbitrary data to share with the thread.
	 */
	data:Dynamic,
	
	/**
	 * Callback for when the thread errors.
	 * 
	 * @param data The thread data.
	 * @param err The error value, if any.
	 */
	onError:(ThreadData, Dynamic)->Void,
	
	/**
	 * Callback for when the thread completes.
	 * 
	 * @param data The thread data.
	 * @param progress The progress value. Defaults to `0`.
	 * @param total The total value. Defaults to `progress` and cannot be less than `progress`.
	 */
	onComplete:(ThreadData, Int, Int)->Void
}

/**
 * Multi-threading helper module.
 * 
 * Warning: Not really thread-safe; you will have to take certain precautions to avoid issues.
 * 
 * Example:
 * ```haxe
 * package exampleMod;
 * 
 * import funkin.modding.module.ModuleHandler;
 * import funkin.modding.module.ScriptedModule;
 * 
 * class ThreadExample extends ScriptedModule {
 *     override public function new() {
 *         super("exampleMod.ThreadExample");
 *     }
 *     
 *     override public function onCreate(event:ScriptEvent):Void {
 *         ModuleHandler.getModule("be.threads.Threads").scriptCall("queueAsync", [
 *             function(data:Dynamic):Void {
 *                 var exampleVar:Int = 0;
 *                 
 *                 // Doing some work...
 *                 for (i in 0...data.data.loopCount) {
 *                     exampleVar += i;
 *                     
 *                     // trace("exampleMod: Did some work! " + Std.string(exampleVar));
 *                     // Don't trace! ^
 *                 }
 *                 
 *                 // For safety, check for panic whenever using code outside of this function	.
 *                 if (!data.panic) {
 *                     ModuleHandler.getModule("exampleMod.ExampleModule").scriptCall("exampleFunction1", [exampleVar]);
 *                 }
 *             },
 *             
 *             {
 *                 data: {
 *                     loopCount: 12345
 *                 },
 *                 
 *                 onComplete: function(data:Dynamic, result:Dynamic):Void {
 *                    trace("exampleMod: Thread example result: " + Std.string(result));
 *                 }
 *             }
 *         ]);
 *     }
 * }
 * ```
 */
class Threads extends ScriptedModule {
	override public function new() {
		super("be.threads.Threads", -2147483648);
	}
	
	/**
	 * All active threads.
	 */
	private var active:Array<ThreadData> = new Array();
	
	/**
	 * Runs a function in a separate thread.
	 * 
	 * Try to avoid interacting with other scripts as much as possible,
	 * but if you really have to do it, check against `data.panic` often.
	 * 
	 * Also, NEVER trace anything to console from a thread!
	 * This leads to unpredictable behavior and some variables being dropped.
	 * 
	 * @param work The function to run in the thread.
	 * @param params Optional parameters for the thread.
	 * @return The thread data object, or `null` if no work was provided.
	 */
	public function queueAsync(work:(ThreadData)->Void, ?params:ThreadParams):Null<ThreadData> {
		if (work == null) {
			return null;
		}
		
		if (params == null) {
			params = {};
		}
		
		var data:ThreadData = {
			future: null,
			data: params.data == null ? {} : params.data,
			
			work: work,
			
			onError: params.onError == null ? function(data:ThreadData):Void {} : params.onError,
			onComplete: params.onComplete == null ? function(data:ThreadData):Void {} : params.onComplete,
			
			panic: false
		};
		
		active.push(data);
		
		data.error = function(err:Dynamic):Void {
			// copied from `lime.app.Promise.error`
			var future:Future = data.future;
			if (future != null && !future.isComplete) {
				future.isError = true;
				future.error = err;
				if (future.__errorListeners != null) {
					for (listener in future.__errorListeners) {
						listener(err);
					}
					
					future.__errorListeners = null;
				}
			}
			
			data.panic = true;
		};
		
		data.complete = function(result:Dynamic):Void {
			// copied from `lime.app.Promise.complete`
			var future:Future = data.future;
			if (future != null && !future.isError) {
				future.isComplete = true;
				future.value = result;
				if (future.__completeListeners != null) {
					for (listener in future.__completeListeners) {
						listener(result);
					}
					
					future.__completeListeners = null;
				}
			}
		};
		
		data.future = new Future(function():Dynamic {
			data.work(data);
			
			return true;
		}, true);
		
		data.future.onError(function(err:Dynamic):Void {
			active.remove(data);
			
			data.panic = true;
			
			data.onError(data, err);
		});
		
		data.future.onComplete(function(result:Dynamic):Void {
			active.remove(data);
			
			data.onComplete(data, result);
		});
		
		return data;
	}
	
	private var pressedReload:Bool = false;
	override public function onUpdate(event:UpdateScriptEvent):Void {
		pressedReload = FlxG.keys.justPressed.F5;
	}
	
	override public function onDestroy(event:ScriptEvent):Void {
		checkPanic();
	}
	
	private function checkPanic():Void {
		if (
			(PlayState.instance != null && PlayState.instance.criticalFailure) ||
			(!FlxG.keys.justPressed.F5 && !pressedReload)
		) {
			return;
		}
		
		while (active.length > 0) {
			active.pop().error("panicking");
		}
	}
}